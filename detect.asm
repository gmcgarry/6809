ACIA	EQU	$D800
ACIACS	EQU	ACIA+0
ACIADA	EQU	ACIA+1

MONITR	EQU	0xE01B

	ORG	$0100
START:
	LDS	#$00FF
	LDAA	#'\r'
	BSR	PUTC
	LDAA	#'\n'
	BSR	PUTC
	LDX	#NOT6809	; POINT TO 6309 STRING
	BSR	DETECT
	BNE	1f		; BRANCH IF 6309 DETECTED
	LDX	#MOT6809	; MODIFY POINTER TO 6809 STRING
1:
	BSR	PUTS		; EMIT THE STRING
	BSR	GETC

;	SWI
	JMP	MONITR

MOT6809:
	.asciz	"MC6809\r\n"
NOT6809:
	.asciz	"HD6309 - Native\r\n"

; Determine whether processor is 6309 or 6809
; Returns Z clear if 6309, set if 6809
DETECT:
	PSHS	D		; SAVE D REGISTER
	FDB	$1043		; 6309 COMD INSRUCTION (COMA on 6809)
	CMPB	1,S		; NOT EQUAL IF 6309
	PULS	D		; RESTORE D REGISTER
	RTS

1:	BSR	PUTC
	INX
PUTS:	LDAA	,X
	BNE	1b
	RTS

PUTC:
	PSHB
1:	LDAB	ACIACS
	ASRB
	ASRB
	BCC	1b
	STAA	ACIADA
	PULB
	RTS

GETC:
1:	LDAA	ACIACS
	ASRA
	BCC	1b
	LDAA	ACIADA
	RTS
